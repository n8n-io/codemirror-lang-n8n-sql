@precedence { dot }

@top Script {
  Statement { element* Semi }*
  Statement { element+ }?
}

@skip { LineComment | BlockComment }

element {
  String |
  Number |
  Bool |
  Null |
  Identifier |
  QuotedIdentifier |
  Bits |
  Bytes |
  Builtin |
  SpecialVar |
  CompositeIdentifier {
    Dot? (QuotedIdentifier | Identifier | SpecialVar | Resolvable) (!dot Dot (QuotedIdentifier | Identifier | SpecialVar | Resolvable))+
  } |
  Keyword |
  Type |
  Operator |
  Punctuation |
  Parens { ParenL element* ParenR } |
  Braces { BraceL element* BraceR } |
  Brackets { BracketL element* BracketR } |
  orphanWrappedResolvable {
    (OrphanSingleQuote Whitespace? Resolvable Whitespace? OrphanSingleQuote)
  } |
  Resolvable |
  Whitespace
}

@tokens {
  OrphanSingleQuote { "'" }

  Resolvable { ResolvableStart resolvableContent* ResolvableEnd }

  ResolvableStart[closedBy="ResolvableEnd"] { "{{" }

  ResolvableEnd[openedBy="ResolvableStart"] { "}}" }

  resolvableContent {
    unicodeChar |
    "}" ![}] |
    "\\}}"
  }

  unicodeChar { $[\u0000-\u007C] | $[\u007E-\u06FF] }
}

@external tokens tokens from "./tokens" {
  LineComment
  BlockComment
  String
  Number
  Bool
  Null
  ParenL[@name="("]
  ParenR[@name=")"]
  BraceL[@name="{"]
  BraceR[@name="}"]
  BracketL[@name="["]
  BracketR[@name="]"]
  Semi[@name=";"]
  Dot[@name="."]
  Operator
  Punctuation
  SpecialVar
  Identifier
  QuotedIdentifier
  Keyword
  Type
  Bits
  Bytes
  Builtin
  Whitespace
}

@detectDelim
